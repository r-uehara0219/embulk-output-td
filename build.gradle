plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "java"
    id "maven" // install jar files to the local repo: $ gradle install
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.3.0"
}

apply from: 'gradle/check.gradle'

repositories {
    mavenCentral()
    jcenter()
}

group = "org.embulk.output.td"
version = "0.7.1"
description = "TreasureData output plugin is an Embulk plugin that loads records to TreasureData read by any input plugins. Search the input plugins by 'embulk-output' keyword."

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

dependencies {
    compileOnly "org.embulk:embulk-core:0.9.23"

    compile("com.treasuredata.client:td-client:0.9.5") {
        // The "org.embulk.embulk-plugins" shows warnings about dependencies
        // because Jackson-related libraries and Guava are not excluded.
        //
        // It is known, and accepted now, because embulk-output-td-0.5.3.gem
        // included them while they conflict with embulk-core's dependencies.
        //
        // TODO: Resolve this.

        exclude group: "com.google.inject", module: "guice"
        exclude group: "com.google.inject.extensions", module: "guice-multibindings"
        exclude group: "org.slf4j", module: "slf4j-api"
        exclude group: "aopalliance", module: "aopalliance"
        exclude group: "javax.inject", module: "javax.inject"
    }

    testCompile "junit:junit:4.13"
    testCompile "org.bigtesting:fixd:1.0.0"
    testCompile "org.embulk:embulk-core:0.9.23:tests"
    testCompile "org.embulk:embulk-standards:0.9.23"
    testCompile "org.embulk:embulk-deps-buffer:0.9.23"
    testCompile "org.embulk:embulk-deps-config:0.9.23"
    testCompile  "org.mockito:mockito-core:1.9.5"
    testCompile "com.github.tomakehurst:wiremock-jre8:2.25.1"
}

embulkPlugin {
    mainClass = "org.embulk.output.td.TdOutputPlugin"
    category = "output"
    type = "td"
}

javadoc {
    options {
        locale = 'en_US'
        encoding = 'UTF-8'
    }
}

// add tests/javadoc/source jar tasks as artifacts to be released
task testsJar(type: Jar, dependsOn: classes) {
    classifier = "tests"
    from sourceSets.test.output
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

// bintray and publishing
bintray {
    // write at your bintray user name and api key to ~/.gradle/gradle.properties file:
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_api_key') ? bintray_api_key : ''

    publications = [ "embulkMavenRelease" ]
    publish = true

    pkg {
        userOrg = 'embulk-output-td'
        repo = 'maven'
        name = project.name
        desc = 'Embulk output plugin for Treasure Data'
        websiteUrl = 'https://github.com/treasure-data/embulk-output-td'
        issueTrackerUrl = 'https://github.com/treasure-data/embulk-output-td/issues'
        vcsUrl = 'https://github.com/treasure-data/embulk-output-td.git'
        licenses = ['Apache-2.0']
        labels = ['embulk', 'java']
        publicDownloadNumbers = true

        version {
            name = project.version
        }
    }
}

publishing {
    publications {
        embulkMavenRelease(MavenPublication) {
            from components.java
            artifact testsJar
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

gem {
    authors = [ "Muga Nishizawa" ]
    email = [ "muga.nishizawa@gmail.com" ]
    summary = "TreasureData output plugin for Embulk"
    homepage = "https://github.com/treasure-data/embulk-output-td"
    licenses = [ "Apache 2.0" ]
}

gemPush {
    host = "https://rubygems.org"
    outputs.upToDateWhen { false }
}
